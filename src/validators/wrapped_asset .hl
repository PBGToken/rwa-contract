mixed wrapped_asset

import { get_current_input, tx } from ScriptContext

struct State {
    supply:      Int    "supply"

    type:        String "type"    // "WrappedAsset", can't change
    venue:       String "venue"   // eg. "Bitcoin" or "Ethereum" or <bank-name>, can't change
    policy:      String "policy"  // eg. "Native" or "USD" or "MXN", can't change
    account:     String "account" // eg. "bc1q23ztqjmh9a32z8j4rstg035y3c3vpfe0la3nck", hash of actual account (+ including secret nonce) if this must remain private, can't change

    ticker:      String "ticker"      // can't change
    name:        String "name"
    description: String "description"
    decimals:    Int    "decimals"    // can't change
    url:         String "url"
    logo:        String "logo"

    quorum:      Int          "quorum"  // can't change
    oracles:     []PubKeyHash "oracles" // can't change
    seed:        TxOutputId   "seed"    // can't change
}

enum Cip68Extra {
    Unused
}

enum Metadata {
    Cip68 {
        state:   State
        version: Int
        extra:   Cip68Extra
    }
    
    func state(self) -> State {
        self.switch{
            x: Cip68 => x.state
        }
    }

    func version(self) -> Int {
        self.switch{
            x: Cip68 => x.version
        }
    }

    func extra(self) -> Cip68Extra {
        self.switch{
            x: Cip68 => x.extra
        }
    }
}

struct Redeemer {
    reserves: Int
}

// TODO: ability to leave const params unset during hl2ts transpilation
const TYPE    = "WrappedAsset"
const VENUE   = "VENUE"
const POLICY  = "POLICY"
const ACCOUNT = "ACCOUNT"

const TICKER      = "TICKER"
const DECIMALS    = 0

const ORACLES      = []PubKeyHash{}
const QUORUM       = (ORACLES.length + 1)/2
const SEED         = TxOutputId::new(TxId::new(#), 0)

const ticker_bytes    = TICKER.encode_utf8()
const user_token_name = Cip67::fungible_token_label + ticker_bytes
const ref_token_name  = Cip67::reference_token_label + ticker_bytes

const own_hash    = Scripts::wrapped_asset
const own_mph     = MintingPolicyHash::from_script_hash(own_hash)
const own_address = Address::new(
    SpendingCredential::new_validator(
        ValidatorHash::from_script_hash(own_hash)
    ),
    Option[StakingCredential]::None
)

const ref_token_asset_class  = AssetClass::new(own_mph, ref_token_name)
const user_token_asset_class = AssetClass::new(own_mph, user_token_name)

func validate_initialization() -> () {
    assert(tx.inputs.any((input: TxInput) -> {
        input.output_id == SEED
    }), "seed not spent")

    ref_utxo = tx.outputs.find((output: TxOutput) -> {
        output.address == own_address
        && output.value.get_safe(ref_token_asset_class) == 1
    })

    metadata = ref_utxo.datum.inline.as_strictly[Metadata]
    state = metadata.state()

    
    assert(state.supply == 0, "supply not initialized at 0")

    assert(state.type == TYPE, "unexpected type")
    assert(state.venue == VENUE, "unexpected venue")
    assert(state.policy == POLICY, "unexpected policy")
    assert(state.account == ACCOUNT, "unexpected account")

    assert(state.ticker == TICKER, "unexpected ticker")
    assert(state.decimals == DECIMALS, "unexpected decimals")

    assert(state.quorum == QUORUM, "unexpected quorum")
    assert(state.seed == SEED, "unexpected seed")

    assert(metadata.version() == 1, "unexpected metadata version")
    assert(metadata.extra() == Cip68Extra::Unused, "unexpected metadata extra values")
}

func signed_by_quorum() -> Bool {
    n_signers = ORACLES.fold((n_signers: Int, key: PubKeyHash) -> {
        n_signers + tx.is_signed_by(key).to_int()
    }, 0)

    n_signers >= QUORUM
}

func validate_state_change(redeemer: Redeemer, input: TxInput) -> () {
    state0 = input.datum.inline.as[Metadata].state()

    output = tx.outputs.find((output: TxOutput) -> {
        output.address == own_address
        && output.value.get_safe(ref_token_asset_class) > 0
    })

    state1 = output.datum.inline.as_strictly[Metadata].state()

    // ensure constant metadata fields don't change
    assert(state1.type == TYPE, "type changed")
    assert(state1.venue == state0.venue, "venue changed")
    assert(state1.policy == state0.policy, "policy changed")
    assert(state1.account == state0.account, "account changed")

    assert(state1.ticker == state0.ticker, "ticker changed")
    assert(state1.decimals == state0.decimals, "decimals changed")

    assert(state1.quorum == QUORUM, "quorum changed")
    assert(state1.oracles == ORACLES, "oracles changed")
    assert(state1.seed == SEED, "seed changed")

    n = tx.minted.get_safe(user_token_asset_class)
    N0 = state0.supply
    N1 = state1.supply

    assert((N1 - N0) == n, "current token supply not updated correctly")
    
    if (n > 0) {
        Redeemer{R} = redeemer

        assert(N1 <= R, "too many tokens minted")
        assert(signed_by_quorum(), "not signed by simple quorum of oracles")
    }
}

func main(args: MixedArgs) -> () {
    args.switch{
        s: Spending => {
            redeemer = s.redeemer.as[Redeemer]

            utxo = get_current_input()

            tokens = utxo.value.get_policy(own_mph)

            if (tokens.is_empty()) {
                // UTXOs that don't contain any tokens from current policy can always be spent.
                // This can be used to remove garbage.
                ()
            } else {
                (name, _) = tokens.head

                if (name == ref_token_name) {
                    validate_state_change(redeemer, utxo)
                } else {
                    error("unexpected token name")
                }
            }
        },
        Other => {
            tokens = tx.minted.get_policy(own_mph)

            (name, qty) = tokens.head
            tail = tokens.tail

            assert(tail.is_empty(), "only one token kind can be minted or burned")

            if (name == user_token_name && qty != 0) {
                // metadata token must be spent, which triggers Spending witness
                assert(tx.inputs.any((input: TxInput) -> {
                    input.address == own_address
                    && input.value.get_safe(ref_token_asset_class) > 0
                }), "ref token not spent")
            } else if (qty == 1 && (name == ref_token_name)) {
                validate_initialization()
            } else {
                error("invalid minted tokens")
            }
        }
    }
}